plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'

    id 'kotlin-parcelize'
    id 'kotlin-kapt'

    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
}

apply from: "$rootDir/constant.gradle"
apply from: "../../../1CorePrivate/config/phonetics/config.gradle"// can remove

android {
    namespace "com.simple.phonetics"
    compileSdkVersion project.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.ipa.english.phonetics"

        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion

        versionCode project.ext.versionCode
        versionName project.ext.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        buildConfigField("String", "BRANCH", "\"${getGitBranchName()}\"")
    }

    signingConfigs {
        keystore {
            keyAlias project.ext.keyAlias // can remove
            keyPassword project.ext.keyPassword // can remove

            storeFile file(project.ext.storeFile) // can remove
            storePassword project.ext.storePassword // can remove
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix project.ext.versionNameDebugSuffix
            resValue "string", "app_name", "EPhoDebug"

            buildConfigField 'boolean', 'DEBUG', 'true'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            resValue "string", "app_name", "EPhonetics"

            buildConfigField 'boolean', 'DEBUG', 'false'
            signingConfig signingConfigs.keystore  // can remove
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility project.ext.javaVersion
        targetCompatibility project.ext.javaVersion
    }
    kotlinOptions {
        jvmTarget = project.ext.javaVersion.toString()
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    dynamicFeatures = [
            ':feature:mlkit', ':feature:campaign', ':feature:ipa_voice_en', ':feature:thanks'
    ]
}

dependencies {

    implementation project(':config:phonetics')// can remove

    implementation project(':library:ipa')
    implementation project(':library:word')
    implementation project(':library:phonetic')
    implementation project(':library:okhttp-cache')

    api koinDependencies.values()
    api jacksonDependencies.values()
    api androidxDependencies.values()
    api retrofit2Dependencies.values()

    api hoanganhtuan95Dependencies.base
    api hoanganhtuan95Dependencies.android

    api hoanganhtuan95Dependencies.size
    api hoanganhtuan95Dependencies.task
    api hoanganhtuan95Dependencies.event
    api hoanganhtuan95Dependencies.state
    api hoanganhtuan95Dependencies.image
    api hoanganhtuan95Dependencies.theme
    api hoanganhtuan95Dependencies.string
    api hoanganhtuan95Dependencies.service

    api hoanganhtuan95Dependencies.analytics
    implementation hoanganhtuan95Dependencies.analytics_log
    implementation hoanganhtuan95Dependencies.analytics_firebase

    api hoanganhtuan95Dependencies.crashlytics
    implementation hoanganhtuan95Dependencies.crashlytics_log
    implementation hoanganhtuan95Dependencies.crashlytics_firebase


    implementation hoanganhtuan95Dependencies.detect_2

    implementation hoanganhtuan95Dependencies.translate_2


    implementation hoanganhtuan95Dependencies.adapter
    kapt hoanganhtuan95Dependencies.adapter_processor
    implementation hoanganhtuan95Dependencies.deeplink
    kapt hoanganhtuan95Dependencies.deeplink_processor
    implementation hoanganhtuan95Dependencies.autobind
    kapt hoanganhtuan95Dependencies.autobind_processor


    implementation platform(firebaseDependencies.bom)
    implementation firebaseDependencies.ads
    implementation firebaseDependencies.perf
    releaseImplementation firebaseDependencies.config
    releaseImplementation firebaseDependencies.analytics
    releaseImplementation firebaseDependencies.crashlytics

    api glideDependencies.glide
    api glideDependencies.glide_transformations

    implementation roomDependencies.room_ktx
    implementation roomDependencies.room_runtime
    kapt roomDependencies.room_compiler

    implementation playDependencies.review
    implementation playDependencies.feature_delivery
    implementation playDependencies.play_services_ads
    implementation playDependencies.play_services_base

    implementation retrofit2Dependencies.logging_interceptor

    implementation googleDependencies.flexbox
    implementation googleDependencies.material
    implementation googleDependencies.auto_service
    kapt googleDependencies.auto_service

    implementation otherDependencies.lottie
    implementation otherDependencies.permissionx
    implementation otherDependencies.keyboardvisibilityevent
}

def getGitBranchName() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}
pluginManagement {
    repositories {
        google()
        jcenter()
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://jitpack.io" }
    }
}

dependencyResolutionManagement {

    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)

    repositories {
        google()
        jcenter()
        mavenLocal()
        mavenCentral()

        maven { url "https://jitpack.io" }
    }
}


rootProject.name = "EPhonetics"


def props = new Properties()
def propsFile = new File(rootDir, "local.properties")

if (propsFile.exists()) {
    propsFile.withInputStream { props.load(it) }
}

def entries = [:]

props.each { key, value ->
    def m = (key =~ /^(.+)\.(path_repo|module|library)$/)
    if (!m.matches()) return

    def prefix = m[0][1]
    def field = m[0][2]

    entries.computeIfAbsent(prefix) { [:] }
    entries[prefix][field] = value
}

def repos = [:]

entries.each { _, data ->
    def pathRepo = data.path_repo
    if (!pathRepo) return

    repos.computeIfAbsent(pathRepo) {
        [modules: []]
    }

    if (data.module && data.library) {
        repos[pathRepo].modules << [
                module : data.module,   // e.g. config:phonetics
                library: data.library   // e.g. group:artifact
        ]
    }
}

repos.each { pathRepo, data ->

    def repoDir = new File(pathRepo)
    if (!repoDir.exists()) {
        return
    }

    includeBuild(pathRepo) {

        if (!data.modules.isEmpty()) {
            dependencySubstitution {

                data.modules.each { m ->
                    substitute module("${m.library}") using project("${m.module}")
                }
            }
        }
    }
}


include ':app'

include ':feature:mlkit'
include ':feature:thanks'
include ':feature:campaign'
include ':feature:ipa_voice_en'

include ':library:ipa'
include ':library:word'
include ':library:phonetic'
include ':library:okhttp-cache'
include ':feature:reminder'
